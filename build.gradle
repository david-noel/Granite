buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype-snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            name = 'minecrell-snapshots'
            url = 'http://repo.minecrell.net/snapshots'
        }
    }

    dependencies {
        classpath 'net.minecrell:VanillaGradle:1.0.1-SNAPSHOT'
        classpath('nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0') {
            exclude module: 'guava'
        }
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

version = '0.1-SNAPSHOT' // TODO: ???

apply plugin: 'net.minecrell.vanilla'
ext.minecraftPlugin = plugins.getPlugin('net.minecrell.vanilla')
apply from: project(':Sponge').file('gradle/implementation.gradle')

minecraft {
    serverTweaker = 'org.spongepowered.granite.launch.server.VanillaServerTweaker'
}

configurations {
    // While we could set this to 'runtime', Eclipse doesn't support runtime dependencies
    // properly so this was always going before 'compile' in Eclipse
    shadow
}

dependencies {
    shadow 'org.ow2.asm:asm-all:5.0.3'
}

// Set main class and classpath for Minecraft server and Launchwrapper
jar {
    manifest {
        attributes(
                'Main-Class': 'org.spongepowered.granite.launch.server.VanillaServerMain',
                'Class-Path': 'bin/minecraft_server.1.8.jar bin/launchwrapper-1.11.jar'
        )
    }
}

shadowJar {
    configurations += [project.configurations.shadow]

    from minecraftPlugin.delayedFile('{SRG_DIR}/notch-srg.srg')
    rename 'notch-srg.srg', 'mappings.srg'

    dependencies {
        include dependency('net.sf.jopt-simple:jopt-simple')
        include dependency('org.ow2.asm:asm-all')

        include dependency('com.typesafe:config')
    }
}
